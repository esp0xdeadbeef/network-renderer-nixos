{ lib, lookup, naming }:

let
  inherit (naming) validInterfaceName;
in
{
  sourceKindForInterface =
    iface:
    if iface ? connectivity && builtins.isAttrs iface.connectivity && builtins.isString (iface.connectivity.sourceKind or null) then
      iface.connectivity.sourceKind
    else if iface ? sourceKind && builtins.isString iface.sourceKind then
      iface.sourceKind
    else
      null;

  attachTargetForInterface =
    { unitName, ifName, iface }:
    let
      matches = lib.filter (
        target:
        (target.unitName or null) == unitName
        && (
          (target.ifName or null) == ifName
          || ((target.renderedIfName or null) == (iface.renderedIfName or null))
          || ((target.interface.renderedIfName or null) == (iface.renderedIfName or null))
          || ((target.hostBridgeName or null) == (iface.hostBridge or null))
        )
      ) lookup.localAttachTargets;
    in
    if builtins.length matches == 1 then
      builtins.head matches
    else if builtins.hasAttr (iface.hostBridge or "") lookup.bridgeNameMap then
      {
        renderedHostBridgeName = lookup.bridgeNameMap.${iface.hostBridge};
        assignedUplinkName = null;
        identity = { };
      }
    else
      throw ''
        s88/CM/network/mapping/container-runtime.nix: could not resolve rendered host bridge for unit '${unitName}', interface '${ifName}'

        iface.hostBridge:
        ${builtins.toJSON (iface.hostBridge or null)}

        available bridgeNameMap keys:
        ${builtins.toJSON (lookup.sortedAttrNames lookup.bridgeNameMap)}

        attachTargets:
        ${builtins.toJSON lookup.localAttachTargets}
      '';

  interfaceNameFromAttachTarget =
    attachTarget:
    if
      attachTarget ? identity
      && builtins.isAttrs attachTarget.identity
      && attachTarget.identity ? portName
      && validInterfaceName attachTarget.identity.portName
    then
      attachTarget.identity.portName
    else
      null;

  interfaceNameFromUpstream =
    iface:
    if iface ? connectivity && builtins.isAttrs iface.connectivity && validInterfaceName (iface.connectivity.upstream or null) then
      iface.connectivity.upstream
    else
      null;
}
